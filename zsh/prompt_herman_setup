_prompt_start_time=$SECONDS
_command_issued=false

pmodload 'helper'

function prompt_print_elapsed_time {
  local end_time=$(( SECONDS - _prompt_start_time ))
  local hours minutes seconds remainder

  if (( end_time >= 3600 )); then
    hours=$(( end_time / 3600 ))
    remainder=$(( end_time % 3600 ))
    minutes=$(( remainder / 60 ))
    seconds=$(( remainder % 60 ))
    print -P "%B%F{red}elapsed time ${hours}h ${minutes}m ${seconds}s%b"
  elif (( end_time >= 60 )); then
    minutes=$(( end_time / 60 ))
    seconds=$(( end_time % 60 ))
    print -P "%B%F{yellow}elapsed time ${minutes}m ${seconds}s%b"
  elif (( end_time >= 1 )); then
    print -P "%B%F{green}elapsed time ${end_time}s%b"
  fi
}

function prompt_herman_precmd {
  if $_command_issued ; then
    prompt_print_elapsed_time
  fi
  _command_issued=false

  git-info
  ruby-info
}


function prompt_herman_preexec {
  _prompt_start_time=$SECONDS
  _command_issued=true
}

function prompt_herman_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling vcs_info before each command.
  add-zsh-hook preexec prompt_herman_preexec
  add-zsh-hook precmd prompt_herman_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format ' %U%F{cyan}%b%f%u'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}●%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}●%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt'  '%b%C%D' \
    'rprompt' ''

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{9}%v%f'

  # Define prompts.
  PROMPT='%F{226}%n%f%F{15}%B@%b%f%F{208}%m%f %B%1~%b$git_info[prompt] %F{10}$%f '
  RPROMPT='$ruby_info[version]'
}

prompt_herman_setup "$@"

# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
